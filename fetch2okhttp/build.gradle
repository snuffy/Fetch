apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-android'

buildscript {
    apply from: '../versions.gradle'
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        // jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

android {
    compileSdkVersion library_compile_version
    buildToolsVersion library_build_tools_version

    defaultConfig {
        minSdkVersion library_min_version
        targetSdkVersion library_target_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }


        debug {

        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation platform("com.squareup.okhttp3:okhttp-bom:$okhttp_version")
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"// No version!
    implementation "com.squareup.okhttp3:okhttp-urlconnection" // No version!

    api project(':fetch2core')

    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test:runner:$test_runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
}

tasks.withType(Javadoc).all {
    enabled = false
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

//simple settings
project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                //setGroupId groupId
                setGroupId "com.tonyodev.fetch2okhttp"
                //setArtifactId artifactId
                setArtifactId "fetch2okhttp"
                version "3.1.6"

                artifact bundleDebugAar
            }
        }
    }
}